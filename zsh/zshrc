#set -x

[ -f .zsh_site ] && source .zsh_site

export SHELL=/bin/$ZSH_NAME

if [ -d ~/bin ]; then
	export PATH=~/bin:${PATH}
fi

if [ `whoami` = "root" ]; then
    export PATH=/sbin:/usr/sbin:/usr/local/sbin:${PATH}
fi

# bring in colors
autoload colors zsh/terminfo
autoload -U compinit
compinit
autoload -U zfinit
zfinit
autoload zcalc
autoload -Uz vcs_info

if [[ "$terminfo[colors]" -ge 8 ]]; then
	colors
fi
for color in black red green yellow blue magenta cyan white; do
    eval attr_$color='$fg[${color}]'
    eval attr_bright_$color='$terminfo[bold]$fg[${color}]'
    eval pr_attr_$color='%{$fg[${color}]%}'
    eval pr_attr_bright_$color='%{$terminfo[bold]$fg[${color}]%}'
done
attr_reset="$terminfo[sgr0]"
attr_bold="$terminfo[bold]"
pr_attr_reset="%{$terminfo[sgr0]%}"
pr_attr_bold="%{$terminfo[bold]%}"

# pseudographics
typeset -A altchar
set -A altchar ${(s..)terminfo[acsc]}
set_charset="$terminfo[enacs]"
shift_in="$terminfo[smacs]"
shift_out="$terminfo[rmacs]"
hbar=${altchar[q]:--}
dbar=${altchar[d]:--}
ulcorner=${altchar[l]:--}
llcorner=${altchar[m]:--}
lrcorner=${altchar[j]:--}
urcorner=${altchar[k]:--}
# same, escaped
pr_set_charset="%{$terminfo[enacs]%}"
pr_shift_in="%{$terminfo[smacs]%}"
pr_shift_out="%{$terminfo[rmacs]%}"

unset LC_ALL
unset LANG
export LANG=en_US.UTF-8
export LC_ALL=en_US.UTF-8
export LC_CTYPE=en_US.UTF-8
export LC_COLLATE=en_US.UTF-8

banner() {
    cat<<EOF
$attr_bright_black${(l.${(e)COLUMNS}..=.)}$attr_reset
${attr_bright_white}Starting `$SHELL --version`
$attr_bright_black${(l.${(e)COLUMNS}..-.)}$attr_reset
${attr_white}Terminal type: $TERM
Hostname:      $HOST
Login name:    $LOGNAME
$attr_bright_black${(l.${(e)COLUMNS}..-.)}$attr_reset
${attr_white}Local time:    `date`
Locale:        $LC_CTYPE
EOF
}

pr_term="$pr_attr_bright_black($pr_attr_reset$pr_attr_cyan%l$pr_attr_reset\
$pr_attr_bright_black)$pr_attr_reset"
pr_time="${pr_attr_cyan}[%T]$pr_attr_reset"
pr_name="%(#.$pr_attr_bright_red.$pr_attr_bright_cyan)%n$pr_attr_reset"
pr_host="$pr_attr_cyan@$pr_attr_bright_white%m$pr_attr_reset"
pr_path="$pr_attr_bold:%U%20<..<%~%<<%u$pr_attr_reset"
pr_user="%(#.$pr_attr_bright_red#.$pr_attr_bright_green\$)$pr_attr_reset"

PS1="$pr_attr_bright_black$pr_set_charset$pr_shift_in$ulcorner$pr_shift_out\
$pr_attr_reset$pr_term$pr_attr_bright_black$pr_shift_in$hbar$pr_shift_out$pr_attr_reset\
${pr_attr_red}[$pr_attr_reset\
$pr_name$pr_host$pr_path$pr_user$pr_attr_reset${pr_attr_red}]\
$pr_attr_bright_black$pr_shift_in%1v$pr_shift_out\
$pr_attr_bright_green%2v$pr_attr_reset\
$pr_time\
$pr_attr_bright_black$pr_shift_in$urcorner $llcorner$pr_shift_out>$pr_attr_reset "
RPS1="${pr_attr_cyan}[%i/%(?.OK.$pr_attr_bright_red%?$pr_attr_reset)${pr_attr_cyan}]\
$pr_attr_reset$pr_attr_bright_black$pr_shift_in$lrcorner$pr_shift_out$pr_attr_reset"

precmd() {
	psvar=()
	vcs_info

	pr_len="$((${COLUMNS} - ${#${(%):---(%l)-[%n@%m]%20<..<%~%<<-[%T]--}} - $#vcs_info_msg_0_))"
	fillbar="\${(l.((${pr_len}))..$hbar.)}"
	psvar[1]=${(e)fillbar}

	[[ -n $vcs_info_msg_0_ ]] && psvar[2]="$vcs_info_msg_0_"

	[[ -t 1 ]] || return
	case $TERM in
		*xterm*|rxvt|rxvt-unicode|(dt|k|E|ml)term) print -Pn "%{\e]2;[%n@%m:%~]\a%}"
		;;
	esac
}

TRAPWINCH() {
	precmd
	zle reset-prompt
}

preexec() {
	[[ -t 1 ]] || return
	case $TERM in
		*xterm*|rxvt|rxvt-unicode|(dt|k|E|ml)term) print -Pn "\e]0;$1 (%n@%m)\a"
		;;
	esac
}

umask 022

export LISTMAX=200
export HISTSIZE=1000000
export SAVEHIST=1000000
export TMPPREFIX=~/tmp
export HISTFILE=~/.zsh_history
export EDITOR=emacs
setopt AlwaysLastPrompt AppendHistory AutoMenu Beep NoHup IncappendHistory
setopt Equals ExtendedHistory ExtendedGlob FunctionArgzero HistBeep ShareHistory
setopt HistIgnoreDups HistIgnoreSpace LongListJobs ListTypes MailWarning
setopt MultIOs ShortLoops RmStarSilent PromptSubst
export ZDOTDIR=~
WORDCHARS='*?_-.[]~=&;!#$%^(){}<>'

if [[ -f $ZDOTDIR/.zsh_aliases ]]	. $ZDOTDIR/.zsh_aliases;

# If GNUls package is installed
if which dircolors>& /dev/null
then
	eval `dircolors`
else
	unalias ls
fi

#eval `lesspipe`

if [[ -f $ZDOTDIR/.zsh_bind ]]		. $ZDOTDIR/.zsh_bind;

hosts=(${${${${(f)"$(<$HOME/.ssh/known_hosts)"}:#[0-9]*}%%\ *}%%,*})
# The following lines were added by compinstall

zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' completer _expand _complete
zstyle ':completion:*' format 'Completing %d:'
zstyle ':completion:*' group-name ''
zstyle ':completion:*:hosts' hosts $hosts
zstyle ':completion:*' insert-unambiguous true
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-prompt '%SAt %p: Hit TAB for more, or the character to insert%s'
zstyle ':completion:*' menu select=long
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' verbose true
zstyle :compinstall filename '~/.zshrc'

banner
